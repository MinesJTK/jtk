plugins {
  id 'java-library'
  id 'maven-publish'
  id 'jacoco'
  id 'signing'
  id 'distribution'
}

description = 'Mines Java Toolkit'
jar.archiveBaseName = 'edu-mines-jtk'

dependencies {
  // This dependency is exported to consumers, that is to say found on their compile classpath.
  api "org.jogamp.jogl:jogl-all-main:$version_jogl"
  api "org.jogamp.gluegen:gluegen-rt-main:$version_gluegen"
  
  // This dependency is used internally, and not exposed to consumers on their own compile classpath.
  implementation "com.googlecode.netlib-java:netlib-java:$version_netlib_java"
  
  testImplementation "org.testng:testng:$version_testng"
  
  testRuntimeOnly "org.testng:testng:$version_testng"
}

distributions {
  main {
    distributionBaseName = 'edu-mines-jtk-all'
  }
}

test {
  useTestNG() {
    options {
      parallel="classes"
      threadCount=5
    }
  }
}

javadoc {
  title = project.description
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}
check.dependsOn jacocoTestReport

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}
