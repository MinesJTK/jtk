description = 'Mines Java Toolkit'

apply plugin: 'java-library-distribution'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'signing'

jar.baseName = 'edu.mines.jtk'

// tag::dependencies[]
dependencies {
  compile     'org.jogamp.jogl:jogl-all-main:2.3.2',
              'org.jogamp.gluegen:gluegen-rt-main:2.3.2',
              'com.googlecode.netlib-java:netlib-java:0.9.3'

  testCompile 'org.testng:testng:6.8'

}
// end::dependencies[]

// tag::distributions[]
distributions {
  main {
    baseName = 'edu.mines.jtk'
  }
}
// end::distributions[]

// tag::test[]
test {
  useTestNG() {
    options {
      parallel="classes"
      threadCount=5
    }
  }
}
// end::test[]

// tag::javadoc[]
javadoc {
  title = project.description
}
// end::javadoc[]

// tag::jacocoTestReport[]
jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}
check.dependsOn jacocoTestReport
// end::jacocoTestReport[]

// tag::javadocJar[]
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from tasks.javadoc.destinationDir
}
// end::javadocJar[]

// tag::sourcesJar[]
task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}
// end::sourcesJar[]

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

signing {
  sign configurations.archives
}

if (project.hasProperty('sonatypeUsername')) {
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
          name 'Mines Java Toolkit'
          packaging 'jar'
          description 'Java packages for science and engineering'
          url 'https://github.com/MinesJTK/jtk/'

          scm {
            url 'scm:git@github.com/MinesJTK/jtk.git'
            connection 'scm:git@github.com/MinesJTK/jtk.git'
            developerConnection 'scm:git@github.com/MinesJTK/jtk.git'
          }

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }

          developers {
            developer {
              id 'dhale'
              name 'Dave Hale'
            }
          }
        }
      }
    }
  }
}
